2.1 データ抽象
==========================

* 合成データを作ったり参照したりするときに、実装の詳細を外に見せず、抽象化したほうが望ましい。
* 作る際の手続きを構成子(constructor)、参照する手続きを選択子(selector)と呼ぶ。

2.1.1 有理数の計算
----------------

有理数の場合には 

```lisp
(define (make-rat n d) (cons n d))
(define (numer x) (car x))
(define (denom x) (cdr x))
```

を構成子、選択子にとることができる。
それを使って各種演算を次のように定義する。

```lisp
(define (add-rat x y)
  (make-rat (+ (* (numer x) (denom y))
               (* (numer y) (denom x)))
            (* (denom x) (denom y))))


(define (sub-rat x y)
  (make-rat (- (* (numer x) (denom y))
               (* (numer y) (denom x)))
            (* (denom x) (denom y))))


(define (mul-rat x y)
  (make-rat (* (numer x) (numer y))
            (* (denom x) (denom y))))


(define (div-rat x y)
  (make-rat (* (numer x) (denom y))
            (* (denom x) (numer y))))


(define (equal-rat? x y)
  (= (* (numer x) (denom y))
     (* (numer y) (denom x))))
```

計算結果はこれで出せる
```lisp
(define (print-rat x)
  (newline)
  (display (numer x))
  (display "/")
  (display (denom x)))
```

約分するように make-rat を改良する
```lisp
(define (make-rat n d)
  (let ((g (gcd n d)))
    (cons (/ n g) (/ d g))))
```

### 問題 2.1

正負両方の引数を扱う改良版make-ratを定義せよ. make-ratは符号を正規化し, 有理数が正なら, 分子, 分母とも正, 有理数が負なら, 分子だけ負とする. 
[回答](exercises/2.1.scm)


2.1.2 抽象の壁
-------------

* 有理数のパッケージは add-rat等だけを使い、add-rat等は make-rat等だけを使い・・という階層構造にする。
* こういう抽象化の壁を使うと、それぞれのレイヤーを分離でき、レイヤーの中では自由に実装できるので便利。
* ある変更が壁の向こう側に影響しない
* 疎結合っていうことかな

### 問題 2.2

平面上の線分を表現する問題を考えよう. 各線分は一対の点: 始発点と終着点で表現されている. 点を使って線分の表現を定義する構成子 make-segmentと選択子 start-segmentと end-segmentを定義せよ. さらに 点は一対の数: x座標とy座標で表現することが出来る. 従ってこの表現を定義する構成子 make-pointと選択子x-pointとy-pointを規定せよ. 最後に選択子と構成子を使い, 引数として線分をとり, 中間点(座標が両端点の座標の平均である点)を返す手続き, midpoint-segmentを定義せよ. その手続きを使ってみるには, 点を印字する方法が必要である.

```lisp
(define (print-point p)
  (newline)
  (display "(")
  (display (x-point p))
  (display ",")
  (display (y-point p))
  (display ")"))
```

2.1.3 データとは何か
------------------

* データとは、構成子と選択子だけじゃない。それらが満たす条件も必要。
* 逆に、条件を満たす構成子と選択子は実のデータと区別がつかない。

```lisp
(define (cons x y)
  (define (dispatch m)
    (cond ((= m 0) x)
          ((= m 1) y)
          (else (error "Argument not 0 or 1 -- CONS" m))))
  dispatch)
(define (car z) (z 0))
(define (cdr z) (z 1))
```

は、実際の cons, car, cdr とまったく同様に使える。
満たすべき条件は、
(car (cons x y)) = x
(cdr (cons x y)) = y
の2つ

* このスタイルをmessage passingという
